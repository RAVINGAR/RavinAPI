allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'pmd'
    apply plugin: 'maven-publish'

    group 'com.ravingarinc.api'
    version '1.5.8'

    repositories {
        gradlePluginPortal()
        mavenLocal()
        mavenCentral()

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven {
            url = ("https://repo.papermc.io/repository/maven-public/")
        }
    }
    dependencies {
        compileOnly('org.jetbrains:annotations:23.0.0') {
            transitive(false)
        }
        //compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
        compileOnly("io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT")
        compileOnly("net.kyori:adventure-text-minimessage:4.15.0")
        compileOnly("net.kyori:text-adapter-bukkit:3.0.6")
        // Todo, if this is 1.18.2, whats the point of supporting older versions?
    }
}

subprojects {

    java {
        sourceCompatibility = JavaVersion.VERSION_16
        targetCompatibility = JavaVersion.VERSION_16
    }


    pmd {
        consoleOutput = true
        toolVersion = '6.41.0'
        rulesMinimumPriority = 5
        ruleSets = []
        ruleSetFiles = files("$rootDir/pmd-rules.xml")
    }


    javadoc {
        options.encoding 'UTF-8'
        options.charSet 'UTF-8'
        options.jFlags('-Xms256m', '-Xmx512m')
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.github.RAVINGAR.RavinAPI'
                artifactId = project.name
                version = project.version

                from components.java
            }
        }
    }

    tasks.withType(PublishToMavenLocal).configureEach {
        onlyIf {
            (publication == publishing.publications.maven && publication.artifactId != 'localtest')
        }
    }

    tasks.withType(PublishToMavenRepository).configureEach {
        onlyIf {
            (publication == publishing.publications.maven && publication.artifactId != 'localtest')
        }
    }
}

