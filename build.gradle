allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    group 'com.ravingarinc.api'
    version '1.1.8'

    repositories {
        gradlePluginPortal()
        mavenCentral()

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
    }
    dependencies {
        compileOnly 'org.jetbrains:annotations:23.0.0'
        compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'pmd'

    sourceCompatibility = 17
    targetCompatibility = 17


    pmd {
        consoleOutput = true
        toolVersion = '6.41.0'
        rulesMinimumPriority = 5
        ruleSets = []
        ruleSetFiles = files("$rootDir/pmd-rules.xml")
    }

    javadoc {
        options.encoding 'UTF-8'
        options.charSet 'UTF-8'
        options.jFlags('-Xms256m', '-Xmx512m')
        options.addStringOption('Xdoclint:none', '-quiet')
        options.links(
                'https://hub.spigotmc.org/javadocs/spigot/'
        )
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

